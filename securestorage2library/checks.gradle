apply plugin: 'checkstyle'
apply plugin: 'pmd'

configurations {
    ktlint
}

check.dependsOn('assembleDebug', 'checkstyle', 'pmd', 'lint', 'ktlint', 'ktlintFormat')

task checkstyle(type: Checkstyle) {
    ignoreFailures true
    configFile file("${project.rootDir}/config/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures true
    ruleSetFiles = files("${project.rootDir}/config/pmd.xml")
    ruleSets = ["java-basic", "java-braces", "java-strings"]

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled true
        html.enabled true
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}